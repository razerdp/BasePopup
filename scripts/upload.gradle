apply plugin: 'com.github.dcendents.android-maven'
apply plugin: "com.jfrog.bintray"

def siteUrl = 'https://github.com/razerdp/BasePopup'
def gitUrl = 'https://github.com/razerdp/BasePopup.git'


group = rootProject.ext.group
version = rootProject.ext.versionName


install {
    repositories.mavenInstaller {
        pom.artifactId = rootProject.ext.candy ? project.ARTIFACT_CANDY_ID : project.ARTIFACT_ID
        pom {
            project {
                packaging 'aar'
                name rootProject.ext.candy ? project.ARTIFACT_CANDY_ID : project.ARTIFACT_ID
                description 'Android下打造通用便捷的PopupWindow'
                url siteUrl

                licenses {
                    license {
                        name 'The Apache Software License, Version 2.0'
                        url 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                    }
                }

                developers {
                    developer {
                        id 'razerdp'
                        name 'razerdp'
                        email 'razerdp123@gmail.com'
                    }
                }

                scm {
                    connection gitUrl
                    developerConnection gitUrl
                    url siteUrl
                }

                if (project.IGNORE_DEPENDENCE) {
                    pom.withXml {
                        asNode().dependencies.'*'.each {
                            it.delete
                        }
                    }
                }
            }
        }
    }
}


task sourcesJar(type: Jar) {
    from android.sourceSets.main.java.srcDirs
    classifier = 'sources'
}
task javadoc(type: Javadoc) {
    source = android.sourceSets.main.java.srcDirs
    classpath += project.files(android.getBootClasspath().join(File.pathSeparator))
    failOnError false
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from javadoc.destinationDir
}

javadoc {
    options {
        encoding "UTF-8"
        charSet 'UTF-8'
        author true
        version true
        links "https://developer.android.google.cn/reference/"
        addStringOption('Xdoclint:none', '-quiet')
    }
}

artifacts {
    archives javadocJar
    archives sourcesJar
}

Properties properties = new Properties()
properties.load(project.rootProject.file('local.properties').newDataInputStream())
bintray {
    user = properties.getProperty("bintray.user")
    key = properties.getProperty("bintray.apikey")
    configurations = ['archives']
    publish = true
    override = true

    pkg {
        repo = "maven"
        name = rootProject.ext.candy ? project.ARTIFACT_CANDY_ID : project.ARTIFACT_ID
        userOrg = 'razerdp'
        websiteUrl = siteUrl
        vcsUrl = gitUrl
        issueTrackerUrl = siteUrl + '/issues'
        labels = ['popup', 'popupwindow', 'basepopup', 'dialog']
        licenses = ["Apache-2.0"]
        publicDownloadNumbers = true
        publish = true
    }
}



